plugins {
    id "com.jfrog.bintray" version "1.7"
}
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

/*
 * Project properties
 */
group 'org.li-soft.gonector'

repositories {
    jcenter()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

/*
 * Building
 */
sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.7+',
    		'org.apache.logging.log4j:log4j-core:2.7+'
    testCompile 'junit:junit:4.11+', 
    			'org.mockito:mockito-core:2.1+' 
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

/*
 * Verification
 */
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport
jar.dependsOn check

/*
 * Publishing
 */
task(checkVersionPresent) << {
	if(project.version == 'unspecified' || project.version == null){
		throw StopExecutionException('Must provide version number with -Pversion="x.y.z"!')
	}
}

publishing {
    publications {
        gonector(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
        }
    }
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['gonector']
	pkg {
		repo = 'lisoft'
		name = project.name
		userOrg = user
		licenses = ['LGPL-3.0']
		vcsUrl = 'https://github.com/EmilyBjoerk/gonector.git'
		labels = ['java', 'go']
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = 'GoNector ' + project.version
			vcsTag = project.version
		}
	}
}

bintrayUpload.dependsOn checkVersionPresent
