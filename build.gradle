plugins {
    id "com.jfrog.bintray" version "1.7"
}
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

// Project properties
group 'org.li-soft.gonector'
version '1.0.0'


publishing {
    publications {
        gonector(MavenPublication) {
            from components.java
            groupId group
            artifactId name
            version version
        }
    }
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['gonector']
	pkg {
		repo = 'lisoft'
		name = project.name
		userOrg = user
		licenses = ['LGPL-3.0']
		vcsUrl = 'https://github.com/EmilyBjoerk/gonector.git'
		labels = ['java', 'go']
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = 'GoNector ' + project.version
			vcsTag = project.version
		}
	}
}


//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    jcenter()
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.7+',
    		'org.apache.logging.log4j:log4j-core:2.7+'
    testCompile 'junit:junit:4.11+', 
    			'org.mockito:mockito-core:2.1+' 
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

/*
findbugs {
    excludeFilter = file("$rootProject.projectDir/findbugsExclude.xml")
}*/

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/view_fx/**',
                              '**/datacache/gamedata/**',
                              '**/datacache/DataCache*'])
        })
    }
}
check.dependsOn jacocoTestReport

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}