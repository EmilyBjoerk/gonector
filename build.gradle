plugins {
    id "com.jfrog.bintray" version "1.7"
}
apply plugin: 'java'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

// Project properties
group 'org.li-soft.gonector'

repositories {
    jcenter()
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-api:2.7+',
    		'org.apache.logging.log4j:log4j-core:2.7+'
    testCompile 'junit:junit:4.11+', 
    			'org.mockito:mockito-core:2.1+' 
}

/*
* Building
*/
sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

/*
* Verification
*/
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

/*
findbugs {
    excludeFilter = file("$rootProject.projectDir/findbugsExclude.xml")
}*/

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/view_fx/**',
                              '**/datacache/gamedata/**',
                              '**/datacache/DataCache*'])
        })
    }
}

check.dependsOn jacocoTestReport
jar.dependsOn check

/*
* Publishing
*/
publishing {
    publications {
        gonector(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
        }
    }
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['gonector']
	//dryRun = true
	pkg {
		repo = 'lisoft'
		name = project.name
		userOrg = user
		licenses = ['LGPL-3.0']
		vcsUrl = 'https://github.com/EmilyBjoerk/gonector.git'
		labels = ['java', 'go']
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = 'GoNector ' + project.version
			vcsTag = project.version
		}
	}
}

task(checkVersionPresent) << {
	//println project.version
	if(project.version == 'unspecified'){
		throw StopExecutionException('Must provide version number with -Pversion="x.y.z"!')
	}
} 

bintrayUpload.dependsOn checkVersionPresent
