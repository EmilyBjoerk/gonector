plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id "com.github.spotbugs" version "4.0.1"
  id 'org.ajoberstar.grgit' version '1.7.2'
  id 'org.ajoberstar.release-opinion' version '1.7.2'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

/*
 * Project properties
 */
group 'org.li-soft.gonector'

repositories {
  jcenter()
}

/*
 * Building
 */
sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
  implementation 'org.apache.logging.log4j:log4j-api:2.13+',
                 'org.apache.logging.log4j:log4j-core:2.13+'
  testImplementation 'junit:junit:4.13+', 
                     'org.mockito:mockito-core:3.3+' 
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

/*
 * Verification
 */
jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }
}

check.dependsOn jacocoTestReport
jar.dependsOn check

/*
 * Publishing
 */

import org.ajoberstar.gradle.git.release.opinion.Strategies

release {
  // Version is inferred from git tags. To do a new release, do:
  //
  // ./gradlew release -Prelease.stage=final -Prelease.scope=major/minor/patch
  versionStrategy Strategies.FINAL
  defaultVersionStrategy Strategies.SNAPSHOT
  tagStrategy {
    generateMessage = { version -> "Version $project.version" }
  }
}

publishing {
  publications {
    gonector(MavenPublication) {
      from components.java
      artifact sourcesJar { classifier "sources" }
      artifact javadocJar { classifier "javadoc" }
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['gonector']
  pkg {
    repo = 'lisoft'
    name = project.name
    userOrg = user
    licenses = ['LGPL-3.0']
    vcsUrl = 'https://github.com/EmilyBjoerk/gonector.git'
    labels = ['java', 'go']
    publicDownloadNumbers = true
    version {
      name = project.version
      desc = 'GoNector ' + project.version
      vcsTag = project.version
    }
  }
}
